/*https://github.com/College-Track/salesforce_objects/tree/master/academic_term*/

/* If a site has modified the dosage requirement, and it is approved take the modified value */
IF(
    AND(
        NOT(ISBLANK(Site_Dosage_Rec_SL__c)),
        ISPICKVAL(Dosage_Change_Approved_SL__c, "Approved")
    ),
    Site_Dosage_Rec_SL__c,
    /* If the term is summer, or the student is a 12th grader, then 0. Else 1 for all students */
    IF(
        OR(
            Term__c = "Summer",
            TEXT(High_School_Grade__c) = "12th Grade"
        ),
        0,
        1
    )
)